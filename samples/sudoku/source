var randState : integer is 1

routine srand(seed : integer) is
    randState := seed
end

routine rand() : integer is
    var a is 1103515245
    var c is 12345
    var m is 1000000007

    randState := (a * randState + c) % m

    return randState
end

routine randInt(min : integer, max : integer): integer is
    var r is rand()
    if r < 0 then
        r := -r
    end
    var range is max - min + 1
    return min + (r % range)
end

routine isValid(
    grid : array[9] array[9] integer,
    row : integer,
    col : integer,
    value : integer
) : boolean is
    for c in 1 .. 9 loop
        if grid[row][c] = value then
            return false
        end
    end

    for r in 1 .. 9 loop
        if grid[r][col] = value then
            return false
        end
    end

    var boxRowStart is (row - 1) / 3 * 3 + 1
    var boxColStart is (col - 1) / 3 * 3 + 1

    for r in boxRowStart .. boxRowStart + 2 loop
        for c in boxColStart .. boxColStart + 2 loop
            if grid[r][c] = value then
                return false
            end
        end
    end

    return true
end

routine shuffleDigits(): array[9] integer is
    var shuffledDigits : array[9] integer
    for i in 1 .. 9 loop
        shuffledDigits[i] := i
    end

    for i in 9 .. 2 reverse loop
        var j is randInt(1, i)
        var temp is shuffledDigits[i]
        shuffledDigits[i] := shuffledDigits[j]
        shuffledDigits[j] := temp
    end
    return shuffledDigits
end

type Position is record
    var r : integer
    var c : integer
end

var positionsArray : array[81] Position
routine findRandomEmptyCell(grid : array[9] array[9] integer) : Position is
    var positionsCount is 0
    for row in 1 .. 9 loop
        for col in 1 .. 9 loop
            if grid[row][col] = 0 then
                positionsArray[positionsCount + 1].r := row
                positionsArray[positionsCount + 1].c := col
                positionsCount := positionsCount + 1
            end
        end
    end
    if positionsCount = 0 then
        var returnResult : Position
        returnResult.r := 0
        returnResult.c := 0
        return returnResult
    end

    var idx is randInt(1, positionsCount)
    return positionsArray[idx]
end

routine makeGridFromArray(arr : array[81] integer) : array[9] array[9] integer is
    var grid : array[9] array[9] integer
    for r in 1 .. 9 loop
        for c in 1 .. 9 loop
            grid[r][c] := arr[1 + (r - 1) * 9 + (c - 1)]
        end
    end
    return grid
end

routine makeArrayFromGrid(grid : array[9] array[9] integer) : array [81] integer is
    var arr : array[81] integer
    for r in 1 .. 9 loop
        for c in 1 .. 9 loop
            arr[1 + (r - 1) * 9 + (c - 1)] := grid[r][c]
        end
    end
    return arr
end

routine solveSudoku(grid : array[9] array[9] integer) : boolean is
    var position is findRandomEmptyCell(grid)

    if position.r = 0 or position.c = 0 then
        return true
    end

    var row is position.r
    var col is position.c

    for digit in 1 .. 9 loop
        if isValid(grid, row, col, digit) then
            grid[row][col] := digit

            if solveSudoku(grid) then
                return true
            end

            grid[row][col] := 0
        end
    end

    return false
end
